buildscript {
    ext.corda_release_version = 'R3.CORDA-3.0.1-DEV-PREVIEW' // "CORDA-3.0.1-DEV-PREVIEW" (Open Source)
    ext.corda_release_distribution = 'com.r3.corda' // "net.corda" for Corda (Open Source)
    ext.corda_gradle_plugins_version = '3.0.3'
    ext.kotlin_version = '1.1.60'
    ext.junit_version = '4.12'
    ext.quasar_version = '0.7.9'
    ext.jolokia_version = '1.3.7'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            credentials {
                username "$artifactoryUser"
                password "$artifactoryPassword"
            }
            url 'https://ci-artifactory.corda.r3cev.com/artifactory/r3-corda-releases'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:publish-utils:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://jitpack.io' }
    maven {
        credentials {
            username "$artifactoryUser"
            password "$artifactoryPassword"
        }
        url 'https://ci-artifactory.corda.r3cev.com/artifactory/r3-corda-releases'
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'maven-publish'

sourceSets {
    main {
        resources {
            srcDir "config/dev"
        }
    }
    test {
        resources {
            srcDir "config/test"
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    cordaCompile "$corda_release_distribution:corda-core:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-finance:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-jackson:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-rpc:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-node-api:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-webserver-impl:$corda_release_version"
    cordaRuntime "$corda_release_distribution:corda:$corda_release_version"
    cordaRuntime "$corda_release_distribution:corda-webserver:$corda_release_version"

    testCompile "$corda_release_distribution:corda-test-utils:$corda_release_version"
    testCompile "$corda_release_distribution:corda-node-driver:$corda_release_version"


    // GraphStream: For visualisation (required by TemplateClientRPC app)
    compile "org.graphstream:gs-core:1.3"
    compile("org.graphstream:gs-ui:1.3") {
        exclude group: "bouncycastle"
    }

    // CorDapp dependencies
    // Specify your cordapp's dependencies below, including dependent cordapps
    cordapp "$corda_release_distribution:corda-finance:$corda_release_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    delete "./build/nodes"
    directory "./build/nodes"
    node {
        name "O=Network Map Service,L=London,C=GB"
        notary = [validating: true]
        p2pPort 10002
        rpcPort 10003
        cordapps = ["$corda_release_distribution:corda-finance:$corda_release_version"]
    }
    node {
        name "O=ParticipantA,L=London,C=GB"
        p2pPort 10005
        rpcPort 10006
        webPort 10007
        cordapps = ["$corda_release_distribution:corda-finance:$corda_release_version"]
        rpcUsers = [[ username: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=ParticipantB,L=New York,C=US"
        p2pPort 10008
        rpcPort 10009
        webPort 10010
        cordapps = ["$corda_release_distribution:corda-finance:$corda_release_version"]
        rpcUsers = [[ username: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=ParticipantC,L=New York,C=US"
        p2pPort 10011
        rpcPort 10012
        webPort 10013
        webPort 10013
        cordapps = ["$corda_release_distribution:corda-finance:$corda_release_version"]
        rpcUsers = [[ username: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}